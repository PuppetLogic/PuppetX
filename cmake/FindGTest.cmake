
set(GTEST_ROOT ${CMAKE_CURRENT_SOURCE_DIR}/../dependencies/gtest)

find_path(GTEST_INCLUDE_DIRS
	NAMES gtest/gtest.h
	HINTS ${GTEST_ROOT}/include
	PATH_SUFFIXES include
)

find_library(GTEST_LIBRARIES
	NAMES gtest
	HINTS ${GTEST_ROOT}/lib
	PATH_SUFFIXES lib
)

find_library(GTEST_MAIN_LIBRARIES
	NAMES gtest_main
	HINTS ${GTEST_ROOT}/lib
	PATH_SUFFIXES lib
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(GTest REQUIRED_VARS GTEST_INCLUDE_DIRS GTEST_LIBRARIES GTEST_MAIN_LIBRARIES)

if(NOT TARGET GTest::GTest)
	find_package(Threads)
	add_library(GTest::GTest INTERFACE IMPORTED)
	set_property(TARGET GTest::GTest PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INCLUDE_DIRS})
	set_property(TARGET GTest::GTest PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_LIBRARIES} Threads::Threads)
	set_property(TARGET GTest::GTest PROPERTY INTERFACE_COMPILE_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_GTEST})
endif()

if(NOT TARGET GTest::Main)
	add_library(GTest::Main INTERFACE IMPORTED)
	set_property(TARGET GTest::Main PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INCLUDE_DIRS})
	set_property(TARGET GTest::Main PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_MAIN_LIBRARIES} GTest::GTest)
	set_property(TARGET GTest::Main PROPERTY INTERFACE_COMPILE_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_GTEST})
endif()
